application zwetser

title "Zwetser"
database "zwetser"

import mobl

load "zwitter.css"

entity Account {
  username : String
  password : String
  updates  : Collection<TwitterUpdate> (inverse: account)
}

entity TwitterUpdate {
  account    : Account (inverse: updates)
  updateId   : Int
  screenName : String
  iconUrl    : String
  text       : String
  mention    : Bool
  favorite   : Bool
  date       : DateTime
}

function updateStream(account : Account) {
   var updates = twitter::getUpdates(account.username, account.password);
   for(twupdate : twitter::Update in updates) {
     if(TwitterUpdate.all().filter("updateId", "=", twupdate.id).one() == null) {
       account.updates.add(TwitterUpdate{
         updateId = twupdate.id,
         screenName = twupdate.user.screen_name,
         iconUrl = twupdate.user.profile_image_url,
         text = twupdate.text,
         date = twupdate.created_at,
         favorite = twupdate.favorited,
         mention = false
       });
     }
   }
}

function updateMentions(account : Account) {
   var updates = twitter::getMentions(account.username, account.password);
   for(twupdate : twitter::Update in updates) {
     if(TwitterUpdate.all().filter("updateId", "=", twupdate.id).one() == null) {
       account.updates.add(TwitterUpdate{
         updateId = twupdate.id,
         screenName = twupdate.user.screen_name,
         iconUrl = twupdate.user.profile_image_url,
         text = twupdate.text,
         date = twupdate.created_at,
         favorite = twupdate.favorited,
         mention = true
       });
     }
   }
}

screen addAccount() {
  header("Add account")
  var account = Account{}
  inputs {
    inputString(account.username, placeholder="Twitter username")
    inputSecret(account.password, placeholder="Twitter password")
  }
  button("Add", onclick={ add(account); screen return; })
}

screen root() {
  init {
    //resetDatabase();
    var account = Account.all().one();
    if(account != null) {
      showUpdates(account);
    }
  }
  header("Accounts")
  topRightButton("Add", onclick={ addAccount(); })
  group {
    list(account in Account.all().order("username", true)) {
      itemArrow(onclick={ showUpdates(account); }) {
        outputString(account.username)
      }
    }
  }
}

screen newTweet(account : Account) {
  var text = ""
  topRightButton("Tweet", onclick={
    async {
      twitter::postUpdate(account.username, account.password, text);
      growl::notify("Tweet posted!");
    }
    screen return;
  })
  backButton("Timeline", onclick={ screen return; })
  basicView("Compose") {
    inputs {
      inputText(text)
    }
  }
}

screen replyTweet(update : TwitterUpdate) {
  var text = "@" + update.screenName + " "
  topRightButton("Reply", onclick={
    async {
      twitter::postReply(update.account.username, update.account.password, update.updateId, text);
      growl::notify("Tweet posted!");
    }
    screen return;
  })
  backButton("Tweet", onclick={ screen return; })
  basicView("Reply @" + update.screenName) {
    inputs {
      inputText(text)
    }
  }
}

screen showUpdates(account : Account) {
  init {
    async {
      updateStream(account);
    }
    async {
      updateMentions(account);
    }
  }
  backButton("Accounts", onclick={ screen return; })
  <img src="lib/img/compose.png" onclick={ newTweet(account); } class="topRight"/>

  template showTweets(coll : Collection<TwitterUpdate>) {
    var limit = 10
    <div id="tweets">
    list(update in coll.limit(limit)) {
      <div class="tweet" onclick={ showUpdate(update); }>
        <span class="icon"><img src=update.iconUrl width="48" height="48"/></span>
        <span class="text">
          <strong>outputString(update.screenName) ": "</strong>
          outputString(update.text)
          <span class="meta">"Posted " outputDate(update.date)</span>
        </span>
      </div>
    }
    altButton("Load more", onclick={ limit = limit + 10; })
    </div>
  }
  template timeline() {
    showTweets(account.updates.filter("mention", "=", false).order("date", false))
  }
  template mentions() {
    showTweets(account.updates.filter("mention", "=", true).order("date", false))
  }
  template search() {
    var phrase = "#mobl"
    var limit = 10
    group {
      inputString(phrase)
    }
    <div id="tweets">
    list(twupdate in twitter::search(phrase).limit(limit)) {
      <div class="tweet">
        <span class="icon"><img src=twupdate.profile_image_url width="48" height="48"/></span>
        <span class="text">
          <strong>outputString(twupdate.from_user) ": "</strong>
          outputString(twupdate.text)
          <span class="meta">"Posted " outputDate(twupdate.created_at)</span>
        </span>
      </div>
    }
    altButton("Load more", onclick={ limit = limit + 10; })
    </div>
  }
  tabset([("All", "lib/toolbar/icon_users.png", timeline), ("Mentions", "lib/toolbar/icon_users.png", mentions), ("Search", "lib/toolbar/icon_users.png", search)])
}

screen showUpdate(update : TwitterUpdate) {
  backButton("Timeline", onclick={ screen return; })
  basicView("Tweet", toolBar=template { toolbarButton("post", onclick={ replyTweet(update); }) }) {
    <div class="bubble">
      outputString(update.text)
      <span class="meta">"Posted " outputDate(update.date)</span>
    </div>
    <div class="postermeta">
      <img src=update.iconUrl width="48" height="48" align="middle"/> " "
      outputString(update.screenName)
    </div>
  }
}