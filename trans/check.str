module check

imports
  include/MoBL
  lookup
  type
  rename

rules

  constraint-error :
    t@SimpleType(_) -> (t, $[Type is not defined: [<write-to-string> t]])
    where not(<lookup-type> t) // no entity for this type
        ; not(_{"TemplateType"} := t)

  constraint-error :
    t@GenericType(_, _) -> (t, $[Type is not defined: [<write-to-string> t]])
    where not(<lookup-type> t) // no entity for this type

  constraint-error :
    Html(tag, _, _, closeTag) -> (closeTag, $[Wrong closing tag])
    where not(tag := closeTag)

  constraint-error :
    t@Var(qid) -> (t, $[Variable not defined: [<write-to-string> t]])
    where not("elements" := qid)
    where not(type-of)

  constraint-error :
    Call(qid, e*) -> (qid, $[Wrong number of arguments, expected [length2] but got [length1].
                             Expected arguments are: [<separate-by(!", "); concat-strings> regular-fargs]])
    where def := <lookup-function> qid
        ; (ExternalFunction(_, _, farg*, _) := def <+ ExternalSyncFunction(_, _, farg*, rt) := def)
        ; regular-args := <filter(not(?NamedExp(_, _)))> e*
        ; regular-fargs := <filter(?FArg(<id>, _))> farg*
        ; length1 := <length> regular-args
        ; length2 := <length> regular-fargs
        ; not(length1 := length2)

  constraint-error :
    Call(qid, e*) -> (t, $[Invalid named arguments: [<separate-by(!", "); concat-strings> invalid-args].
                           Valid named arguments: [<separate-by(!", "); concat-strings> named-fargs]])
    where def := <lookup-function> qid
        ; (ExternalFunction(_, _, farg*, _) := def <+ ExternalSyncFunction(_, _, farg*, rt) := def)
        ; named-fargs := <filter(?FArgOptional(<id>, _, _)); strip-annos> farg*
        ; invalid-args := <filter(\ NamedExp(x, _) -> x where not(<fetch(?x)> named-fargs) \)> e*
        ; [t|_] := invalid-args

  constraint-error :
    t@Choice(_, _, _) -> (t, $[Could not determine type of: [<write-to-string> t]])
    where not(type-of)

  constraint-error :
    t@Brackets(_) -> (t, $[Could not determine type of: [<write-to-string> t]])
    where not(type-of)

  constraint-error :
    TemplateCall(e, e*, elem*) -> (e, $[Expression does not return a template type])
    where t := <type-of> e
    where not(<is-template-type> t)

  constraint-error :
    TemplateCall(e@Var(qid@QId(_, _)), e*, _) -> (e, $[Wrong number of arguments, expected [length2] but got [length1].
                                                       Expected arguments are: [<separate-by(!", "); concat-strings> regular-fargs]])
    where ExternalTemplate(_, _, farg*) := <lookup-template> qid
        ; regular-args := <filter(not(?NamedExp(_, _)))> e*
        ; regular-fargs := <filter(?FArg(<id>, _))> farg*
        ; length1 := <length> regular-args
        ; length2 := <length> regular-fargs
        ; not(length1 := length2)

  constraint-error :
    TemplateCall(e@Var(qid@QId(_, _)), e*, _) -> (t, $[Invalid named arguments: [<separate-by(!", "); concat-strings> invalid-args].
                                                       Valid named arguments: [<separate-by(!", "); concat-strings> named-fargs]])
    where ExternalTemplate(_, _, farg*) := <lookup-template> qid
        ; named-fargs := <filter(?FArgOptional(<id>, _, _)); strip-annos> farg*
        ; invalid-args := <filter(\ NamedExp(x, _) -> x where not(<fetch(?x)> named-fargs) \)> e*
        ; [t|_] := invalid-args

  constraint-error :
    t@FieldAccess(e, x) -> (t, $[Field [x] not defined])
    where <type-of> e
    where not(type-of)

  constraint-error :
    t@Call(qid, arg*) -> (t, $[Function not defined])
    where <map(type-of)> arg*
    where not(type-of)

  constraint-error :
    t@MethodCall(e, x, arg*) -> (t, $[No such method '[x]' defined])
    where <type-of> e
    where <map(type-of)> arg*
    where not(type-of)

  constraint-error :
    Assignment(lhs, e) -> (lhs, $[No such variable '[lhs]' defined])
    where <is-string> lhs
    where not(<type-of> Var(lhs))

  constraint-warning:
    Entity(_, qid, _, _) -> (x, $[Entity names must start with a capital])
    where
      if <is-string> qid then
        x := qid
      else
        QId(_, x) := qid
      end
      ; not(<explode-string; Hd; is-upper> x)

/*  constraint-note:
    Module(x @ "example", _) -> (x, $[This is just an example program in the "entities" language
                                      (this note is defined in trans/check.str) ])
*/
  constraint-note = fail
