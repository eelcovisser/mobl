module generation/data

imports
  include/MoBL
  generation/emit
  generation/ui
  generation/action
  generation/sql
  lookup

strategies
  entities-to-js =
    {| OneToMany, SearchableProperties
     : map(entity-to-js)
     ; <map(one-to-many-to-js)> <bagof-OneToMany>
     ; <map(searchable-to-js)> <bagof-SearchableProperties>
     //; <emit> $[persistence.schemaSync();]
     |}

  entity-to-js :
    Entity(_, qid, super, prop*) -> <emit> $[
    [varname] = persistence.define('[<qid-to-html> qid]', {
      [props]
    });
    ]
    with props := <filter(property-to-js(|qid)); separate-by(!",\n"); concat-strings> prop*
       ; if <is-string> qid then
           varname := $[var [qid]]
         else
           varname := <qid-to-js> qid
         end

  property-to-js(|ent) :
    Property(_, x, t@SimpleType(_), anno*) -> $['[x]': '[sqlType]']
    where not(<is-entity-type> t)
    with sqlType := <sql-type <+ debug(!"Could not convert to SQL: "); fail> t
       ; if [_] := <filter(?SimpleAnno("searchable"))> anno* then
           rules ( SearchableProperties :+= (ent, x) )
         end

  property-to-js(|ent) :
    Property(_, x, GenericType(coll, [t]), anno*) -> <fail>
    where <filter(?InverseAnno(inv))> anno*
        ; rules ( OneToMany :+= (SimpleType(ent), x, t, inv) )

  one-to-many-to-js :
    (SimpleType(from), field, SimpleType(to), invField) -> <emit> $[[<qid-to-js> from].hasMany('[field]', [<qid-to-js> to], '[invField]');
]

  searchable-to-js :
    (ent, field) -> <emit> $[[<qid-to-js> ent].textIndex('[field]');
]